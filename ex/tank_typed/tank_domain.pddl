(define (domain tank-domain)
(:requirements :fluents :durative-actions :duration-inequalities :negative-preconditions :typing)
(:types tank bucket)
(:predicates (draining ?t - tank) (filling ?b - bucket))
(:functions (volumetank ?t - tank) (volumebuck ?b - bucket) (drain-time ?t - tank) 
	(flow-constant ?t - tank) (capacity ?b - bucket) (sqrtvolinit ?t - tank) (sqrtvol ?t - tank))

(:durative-action fill-bucket
:parameters (?b - bucket ?t - tank)
:duration (<= ?duration (/ (sqrtvolinit ?t) (flow-constant ?t)))
:condition (and (over all (<= (volumebuck ?b) (capacity ?b)))
  (at start (not (draining ?t)))
        (at start (not (filling ?b))))
:effect (and (at start (assign (drain-time ?t) 0))
    (at start (assign (sqrtvol ?t) (sqrtvolinit ?t)))
    (at start (draining ?t))
    (at start (filling ?b))
    (increase (drain-time ?t) (* #t 1))
    (decrease (volumetank ?t) (* #t (* (* 2 (flow-constant ?t))
            (- (sqrtvolinit ?t) (* (flow-constant ?t) (drain-time ?t))))))
    (decrease (sqrtvol ?t) (* #t (flow-constant ?t)))
    (increase (volumebuck ?b) (* #t (* (* 2 (flow-constant ?t))
            (- (sqrtvolinit ?t) (* (flow-constant ?t) (drain-time ?t))))))
    (at end (assign (sqrtvolinit ?t) (sqrtvol ?t)))
    (at end (not (draining ?t)))
    (at end (not (filling ?b))))))